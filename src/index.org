#+SETUPFILE: ./setup.org
#+TITLE: thi.ng/ndarray
#+AUTHOR: Karsten Schmidt
#+EMAIL: k@thi.ng

* Contents                                                         :toc_3_gh:
 - [[#about-the-project][About the project]]
     - [[#description][Description]]
     - [[#example-usage][Example usage]]
         - [[#transpose-matrix][Transpose matrix]]
         - [[#reverse-direction-in-any-dimension][Reverse direction in any dimension]]
         - [[#skip-items-in-any-dimension][Skip items in any dimension]]
         - [[#extract-slices-of-a-volume][Extract slices of a volume]]
         - [[#fill-subsection-of-an-array][Fill subsection of an array]]
         - [[#undirected-graph-queries-via-adjacency-matrix][Undirected graph queries via adjacency matrix]]
 - [[#namespaces][Namespaces]]
 - [[#tests][Tests]]
 - [[#project-definition][Project definition]]
     - [[#injected-properties][Injected properties]]
     - [[#dependencies][Dependencies]]
         - [[#runtime][Runtime]]
         - [[#development][Development]]
     - [[#leiningen-coordinates][Leiningen coordinates]]
     - [[#building-this-project][Building this project]]
         - [[#testing][Testing]]
     - [[#leiningen-project-file][Leiningen project file]]
     - [[#clojurescript-html-harness][ClojureScript HTML harness]]
     - [[#accessing-library-version-during-runtime][Accessing library version during runtime]]
         - [[#version-namespace][Version namespace]]
     - [[#release-history][Release history]]
     - [[#contributors][Contributors]]
     - [[#license][License]]

* About the project

** Description

This library is a Clojure/Clojurescript port of Mikola Lysenko's
[[https://github.com/scijs/ndarray][ndarray]] core JS library with extended features to make more sense in a
Clojure context.

** Example usage

#+BEGIN_SRC clojure
  (require '[thi.ng.ndarray.core :as nd])
#+END_SRC

*** Transpose matrix

The =ndarray= constructor automatically coerces a Clojure seq into the
correct array type (if not given as array already). =transpose= allows
swapping the axis order without any copying.

#+BEGIN_SRC clojure
  (def a (nd/ndarray :float64 (range 16) [4 4]))
  (seq a)
  ;; => (0.0 1.0 2.0 3.0 4.0 5.0 6.0 7.0 8.0 9.0 10.0 11.0 12.0 13.0 14.0 15.0)
  (seq (nd/transpose a 1 0))
  ;; => (0.0 4.0 8.0 12.0 1.0 5.0 9.0 13.0 2.0 6.0 10.0 14.0 3.0 7.0 11.0 15.0)
#+END_SRC

*** Reverse direction in any dimension

Likewise, step order can be independently flipped/manipulated in any dimension...

#+BEGIN_SRC clojure
  (def a (nd/ndarray :float64 (range 16) [4 4]))
  (seq (nd/step a -1 nil))
  ;; => (12.0 13.0 14.0 15.0 8.0 9.0 10.0 11.0 4.0 5.0 6.0 7.0 0.0 1.0 2.0 3.0)
  (seq (nd/step a nil -1))
  ;; => (3.0 2.0 1.0 0.0 7.0 6.0 5.0 4.0 11.0 10.0 9.0 8.0 15.0 14.0 13.0 12.0)
#+END_SRC

*** Skip items in any dimension

#+BEGIN_SRC clojure
  (def a (nd/ndarray :float64 (range 16) [4 4]))
  (seq (nd/step a 2 2))
  ;; => (0.0 2.0 8.0 10.0)

  ;; ...optionally with offset
  (-> a (nd/truncate-l 0 1) (nd/step 2 2) seq)
  ;; => (1.0 3.0 9.0 11.0)
#+END_SRC

*** Extract slices of a volume

#+BEGIN_SRC clojure
  (def a (nd/ndarray :float32 (range 27) [3 3 3]))
  (seq (nd/pick a 2 nil nil))
  ;; => (18.0 19.0 20.0 21.0 22.0 23.0 24.0 25.0 26.0)
  (seq (nd/pick a nil 2 nil))
  ;; => (6.0 7.0 8.0 15.0 16.0 17.0 24.0 25.0 26.0)
  (seq (nd/pick a nil nil 2))
  ;; => (2.0 5.0 8.0 11.0 14.0 17.0 20.0 23.0 26.0)
  (seq (nd/pick a nil 0 0))
  ;; (0.0 9.0 18.0)
#+END_SRC

*** Fill subsection of an array

#+BEGIN_SRC clojure
  (def a (nd/ndarray :int8 (byte-array 25) [5 5]))
  (dorun
   (for [i (-> a (nd/truncate-h 4 4) (nd/truncate-l 1 1) nd/index-seq)]
     (nd/set-at-index a i 1)))
  (seq a)

  ;; (0 0 0 0 0
  ;;  0 1 1 1 0
  ;;  0 1 1 1 0
  ;;  0 1 1 1 0
  ;;  0 0 0 0 0)
#+END_SRC

*** Undirected graph queries via adjacency matrix

A slightly larger example to demonstrate connected component queries on a graph:

#+BEGIN_SRC clojure
  (defn adjacency-mat
    [ids]
    (let [n (count ids)]
      (nd/ndarray :boolean (repeat (* n n) false) [n n])))

  (defn add-edge
    [index mat [a b]]
    (-> mat
        (nd/set-at (index a) (index b) true)
        (nd/set-at (index b) (index a) true)))

  (defn edge?
    [index mat [a b]]
    (nd/get-at mat (index a) (index b)))

  (defn neighbors
    [index rev-index mat x]
    (sequence
     (comp
      (map-indexed (fn [i rel] (if rel (rev-index i))))
      (filter identity))
     (nd/pick mat (index x) nil)))

  (defn all-connected
    ([index rev-index mat q]
     (all-connected index rev-index mat #{} #{} [q]))
    ([index rev-index mat acc seen q]
     (let [n    (->> q
                     (filter (complement seen))
                     (mapcat #(neighbors index rev-index graph %)))
           acc' (into acc n)]
       (if (= acc acc')
         acc
         (recur index rev-index mat acc' (into seen q) n)))))

  (def index {:a 0 :b 1 :c 2 :d 3 :e 4 :f 5})
  (def rev-index (reduce-kv #(assoc % %3 %2) {} index))

  (def graph
    (reduce
     (partial add-edge index)
     (adjacency-mat index)
     [[:a :b] [:b :d] [:d :c] [:e :f]]))

  (edge? index graph [:b :a]) ;; => true
  (edge? index graph [:a :c]) ;; => false
  (neighbors index rev-index graph :c) ;; => (:a :d)

  (all-connected index rev-index graph :a) ;; => #{:c :b :d :a}
  (all-connected index rev-index graph :f) ;; => #{:e :f}
#+END_SRC

* Namespaces

- [[./core.org][thi.ng.ndarray.core]]
- [[./contours.org][thi.ng.ndarray.contours]]

* Tests

- [[../test/core.org][thi.ng.ndarray.test.core]]

* Project definition

** Injected properties                                             :noexport:

#+BEGIN_SRC clojure :noweb-ref version
0.2.0-SNAPSHOT
#+END_SRC
#+BEGIN_SRC clojure :exports none :noweb-ref project-url
http://thi.ng/ndarray
#+END_SRC
#+BEGIN_SRC clojure :exports none :noweb yes :noweb-ref cljs-artefact-path
target/ndarray-<<version>>.js
#+END_SRC

** Dependencies
*** Runtime
**** [[https://github.com/clojure/clojure][Clojure]]
#+BEGIN_SRC clojure :noweb-ref dep-clj
[org.clojure/clojure "1.7.0-RC1"]
#+END_SRC
**** [[https://github.com/clojure/clojurescript][ClojureScript]]
#+BEGIN_SRC clojure :noweb-ref dep-cljs
[org.clojure/clojurescript "0.0-3308"]
#+END_SRC
**** [[http://thi.ng/typedarrays][thi.ng/typedarrays]]
#+BEGIN_SRC clojure :noweb-ref dep-arrays
[thi.ng/typedarrays "0.1.2"]
#+END_SRC
**** [[http://thi.ng/math][thi.ng/math]]
#+BEGIN_SRC clojure :noweb-ref dep-math
[thi.ng/math "0.1.3"]
#+END_SRC
*** Development
**** [[https://github.com/cemerick/clojurescript.test][clojurescript.test]]
#+BEGIN_SRC clojure :noweb-ref dep-cljs-test
[com.cemerick/clojurescript.test "0.3.3"]
#+END_SRC
**** [[https://github.com/emezeske/lein-cljsbuild][Cljsbuild]]
#+BEGIN_SRC clojure :noweb-ref dep-cljsbuild
[lein-cljsbuild "1.0.6"]
#+END_SRC
**** [[https://github.com/weavejester/lein-auto][lein-auto]]
#+BEGIN_SRC clojure :noweb-ref dep-autotest
[lein-auto "0.1.2"]
#+END_SRC
**** [[https://github.com/hugoduncan/criterium][Criterium]]
#+NAME: dep-criterium
#+BEGIN_SRC clojure
[criterium "0.4.3"]
#+END_SRC

** Leiningen coordinates

#+BEGIN_SRC clojure :noweb yes :noweb-ref lein-coords
  [thi.ng/ndarray "0.2.0-SNAPSHOT"]
#+END_SRC

** Building this project

This project is written in a literate programming format and requires
[[https://www.gnu.org/software/emacs/][Emacs]] & [[http://orgmode.org][Org-mode]] to generate usable source code. Assuming both tools
are installed, the easiest way to generate a working project is via
command line (make sure =emacs= is on your path or else edit its path
in =tangle.sh=):

#+BEGIN_SRC bash
  git clone https://github.com/thi.ng/ndarray.git
  cd ndarray
  ./tangle.sh src/*.org test/*.org
#+END_SRC

Tangling is the process of extracting & combining source blocks from
=.org= files into an actual working project/source tree. Once tangling
is complete, you can =cd= into the generated project directory
(=babel=) and then use =lein= as usual.

*** Testing

The =project.clj= file defines an alias to trigger a complete build &
tests for both Clojure & Clojurescript. Since PhantomJS (the usual
test runner for other thi.ng projects) doesn't support typed arrays,
the =cleantest= command will run the Clojurescript tests in your
default browser and test results are displayed in the dev console.

#+BEGIN_SRC bash
  cd babel
  lein cleantest

  ;; or use auto test runner via lein-auto
  lein auto test
#+END_SRC

Tangling this file will also generate a small HTML harness for the
resulting JS file and will be placed in the main folder
(=babel/index.html=), allowing for further experimentation in the
browser.

** Leiningen project file                                          :noexport:

#+BEGIN_SRC clojure :tangle ../babel/project.clj :noweb yes :mkdirp yes :padline no
  (defproject thi.ng/ndarray "<<version>>"
    :description  "ndarray for Clojure/Clojurescript"
    :url          "<<project-url>>"
    :license      {:name "Apache Software License 2.0"
                   :url "http://www.apache.org/licenses/LICENSE-2.0"
                   :distribution :repo}
    :scm          {:name "git"
                   :url "git@github.com:thi-ng/ndarray.git"}

    :min-lein-vesion "2.4.0"

    :dependencies [<<dep-clj>>
                   <<dep-cljs>>
                   <<dep-arrays>>
                   <<dep-math>>]

    :plugins      [<<dep-autotest>>]

    :profiles     {:dev {:dependencies [<<dep-criterium>>]
                         :plugins [<<dep-cljsbuild>>
                                   <<dep-cljs-test>>]
                         :global-vars {*warn-on-reflection* true}
                         :jvm-opts ^:replace []
                         :aliases {"cleantest" ["do" "clean," "test," "cljsbuild" "test"]}}}

    :auto         {:default {:file-pattern #"\.(clj|cljs|cljc)$"}}

    :cljsbuild    {:builds [{:id "simple"
                             :source-paths ["src" "test"]
                             :compiler {:output-to "<<cljs-artefact-path>>"
                                        :optimizations :whitespace
                                        :pretty-print true}}]
                   :test-commands {"unit-tests" ["open" :runner "index.html"]}}

    :pom-addition [:developers [:developer
                                [:name "Karsten Schmidt"]
                                [:url "http://postspectacular.com"]
                                [:timezone "0"]]])
#+END_SRC

** ClojureScript HTML harness                                      :noexport:

#+BEGIN_SRC html :tangle ../babel/index.html :noweb yes :mkdirp yes :padline no
  <!DOCTYPE html>
  <html lang="en">
    <head>
      <title><<lein-coords>> test</title>
    </head>
    <body>
      <script type="text/javascript" src="<<cljs-artefact-path>>"></script>
    </body>
  </html>
#+END_SRC

** Accessing library version during runtime

The autogenerated namespace =thi.ng.ndarray.version= contains a single
symbol =version= holding the version string defined above:

#+BEGIN_SRC clojure :noweb yes
  (use '[thi.ng.ndarray.version])
  
  (prn version)
  ; "<<version>>"
#+END_SRC

*** Version namespace                                              :noexport:

#+BEGIN_SRC clojure :tangle ../babel/src/thi/ng/ndarray/version.cljc :noweb yes :mkdirp yes :padline no :exports none
  (ns thi.ng.ndarray.version)

  (def version "<<version>>")
#+END_SRC

** Release history

| *Version* | *Released* | *Description*        | *Lein coordinates*         | *Tagged Github URL* |
|-----------+------------+----------------------+----------------------------+---------------------|
|     0.1.0 | 2015-05-31 | initial test release | =[thi.ng/ndarray "0.1.0"]= | [[https://github.com/thi-ng/typedarrays/tree/0.1.0][0.1.0]]               |

** Contributors

| *Name*          | *Role*                          | *Website*                  |
|-----------------+---------------------------------+----------------------------|
| [[k@thi.ng][Karsten Schmidt]] | initiator & principal developer | http://postspectacular.com |

I've got a fairly detailed roadmap and task list to implement over the
coming months, but am always happy to receive feedback & suggestions
and have issues filed. Once the core engine is more refined I'll be
gladly welcoming other contributions. Thanks for understanding!

** License

This project is open source and licensed under the [[http://www.apache.org/licenses/LICENSE-2.0][Apache Software License 2.0]].
